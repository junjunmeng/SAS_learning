{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "4e6c4f48",
   "metadata": {},
   "source": [
    "### Step 1: inculde the setup of source dataset, metadata and macros"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "17d95dc5",
   "metadata": {},
   "outputs": [],
   "source": [
    "*---------------------------------------------------------------*;\n",
    "* ADAE.sas creates the ADaM ADAE-structured data set\n",
    "* for AE data (ADAE), saved to the ADaM libref.\n",
    "*---------------------------------------------------------------*;\n",
    "\n",
    "%include \"/home/junmeng0/02-Implementing CDISC Using SAS/chapter07/setup.sas\";"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5370f262-249c-4b7c-8af0-33fb76dbf34c",
   "metadata": {},
   "source": [
    "#### 1.1 Macro: setup.sas"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8e6c0155",
   "metadata": {},
   "outputs": [],
   "source": [
    "**** defines common librefs and SAS options.;\n",
    "\n",
    "%let sasroot=%sysfunc(sysget(SASROOT));\n",
    "%let sysin = %sysfunc(getoption(sysin));\n",
    "\n",
    "options ls=256 nocenter extendobscounter=NO\n",
    "        mautosource \n",
    "        sasautos=(\"/home/junmeng0/02-Implementing CDISC Using SAS/macro\") ;\n",
    "        \n",
    "                    \n",
    "%include \"/home/junmeng0/02-Implementing CDISC Using SAS/macro/xpt_macros.sas\";\n",
    "libname sdtm    \"/home/junmeng0/02-Implementing CDISC Using SAS/chapter03\";\n",
    "libname adam    \"/home/junmeng0/02-Implementing CDISC Using SAS/chapter07\";\n",
    "\n",
    "\n",
    "proc format;\n",
    "        value _0n1y 0 = 'N'\n",
    "                    1 = 'Y'\n",
    "        ;                    \n",
    "        value avisitn 1 = '3'\n",
    "                      2 = '6'\n",
    "        ;                      \n",
    "        value popfl 0 - high = 'Y'\n",
    "                    other = 'N'\n",
    "        ;                    \n",
    "        value $trt01pn  'Analgezia HCL 30 mg' = '1'\n",
    "                        'Placebo'             = '0'\n",
    "        ;\n",
    "        value agegr1n 0 - 54 = \"1\"\n",
    "                      55-high= \"2\"\n",
    "        ;                      \n",
    "        value agegr1_ 1 = \"<55 YEARS\"\n",
    "                      2 = \">=55 YEARS\"\n",
    "        ;                      \n",
    "        value $aereln  'NOT RELATED'        = '0'\n",
    "                       'POSSIBLY RELATED'   = '1'\n",
    "                       'PROBABLY RELATED'   = '2'\n",
    "        ;\n",
    "        value $aesevn  'MILD'               = '1'\n",
    "                       'MODERATE'           = '2'\n",
    "                       'SEVERE'             = '3'\n",
    "        ;                                              \n",
    "        value relgr1n 0 = 'Not related'\n",
    "                      1 = 'Related'\n",
    "        ;                       \n",
    "        value evntdesc 0 = 'PAIN RELIEF'\n",
    "                       1 = 'PAIN WORSENING PRIOR TO RELIEF'\n",
    "                       2 = 'PAIN ADVERSE EVENT PRIOR TO RELIEF'\n",
    "                       3 = 'COMPLETED STUDY PRIOR TO RELIEF'\n",
    "        ;                    \n",
    "run;\n",
    " "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "de3bbd1e",
   "metadata": {},
   "source": [
    "### Step2. Create empty ADAE dataset"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7e255316",
   "metadata": {},
   "outputs": [],
   "source": [
    "**** CREATE EMPTY ADAE DATASET CALLED EMPTY_ADAE;\n",
    "options mprint ;*symbolgen;\n",
    "%let metadatafile=/home/junmeng0/02-Implementing CDISC Using SAS/chapter07/ADAM_METADATA.xls;\n",
    "\n",
    "%make_empty_dataset(metadatafile=&metadatafile,dataset=ADAE);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "269a535e-bb1b-4919-898b-bc688e0fecba",
   "metadata": {},
   "source": [
    "#### 2.1 macro make_empty_dataset"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0d892417-7c0d-436c-8b53-aee3fc187df0",
   "metadata": {},
   "outputs": [],
   "source": [
    "/*---------------------------------------------------------------*;\n",
    " make_empty_dataset.sas creates a zero record dataset based on a  dataset metadata spreadsheet.  The dataset created is called * EMPTY_** \n",
    " where \"**\" is the name of the dataset.  \n",
    "This macro also creates a global macro variable called **KEEPSTRING that holds the dataset variables desired and listed in the order they should appear.  [The variable order is dictated by VARNUM in the metadata spreadsheet\n",
    "\n",
    "MACRO PARAMETERS:\n",
    "- metadatafile = the MS Excel file containing the dataset metadata\n",
    "- dataset = the dataset or domain name you want to extract\n",
    "*---------------------------------------------------------------*/;\n",
    "%macro make_empty_dataset(metadatafile=,dataset=);\n",
    "\n",
    "    proc import \n",
    "        datafile=\"&metadatafile\"\n",
    "        out=_temp \n",
    "        dbms=xls\n",
    "        replace;\n",
    "        sheet=\"VARIABLE_METADATA\";\n",
    "    run;\n",
    "\n",
    "    ** sort the dataset by expected specified variable order;\n",
    "    proc sort\n",
    "      data=_temp;\n",
    "\t  where domain = \"&dataset\";\n",
    "        by varnum;\t  \n",
    "    run;\n",
    "\n",
    "    ** create keepstring macro variable and load metadata \n",
    "    ** information into macro variables;\n",
    "    %global &dataset.KEEPSTRING;\n",
    "    data _null_;\n",
    "      set _temp nobs=nobs end=eof;\n",
    "\n",
    "        if _n_=1 then\n",
    "          call symput(\"vars\", compress(put(nobs,3.)));\n",
    "    \n",
    "        call symputx('var'    || compress(put(_n_, 3.)), variable);\n",
    "        call symputx('label'  || compress(put(_n_, 3.)), label);\n",
    "        call symputx('length' || compress(put(_n_, 3.)), put(length, 3.));\n",
    "\n",
    "\n",
    "        ** valid ODM types include TEXT, INTEGER, FLOAT, DATETIME, \n",
    "        ** DATE, TIME and map to SAS numeric or character;\n",
    "        if upcase(type) in (\"INTEGER\", \"FLOAT\") then\n",
    "          call symputx('type' || compress(put(_n_, 3.)), \"\");\n",
    "        else if upcase(type) in (\"TEXT\", \"DATE\", \"DATETIME\", \"TIME\") then\n",
    "          call symputx('type' || compress(put(_n_, 3.)), \"$\");\n",
    "        else\n",
    "          put \"ERR\" \"OR: not using a valid ODM type.  \" type=;\n",
    "\n",
    "\n",
    "        ** create **KEEPSTRING macro variable;\n",
    "        length keepstring $ 32767;\t \n",
    "        retain keepstring;\t\t\n",
    "        keepstring = compress(keepstring) || \"|\" || left(variable); \n",
    "        if eof then\n",
    "          call symputx(upcase(compress(\"&dataset\" || 'KEEPSTRING')), \n",
    "                       left(trim(translate(keepstring,\" \",\"|\"))));\n",
    "    run;\n",
    "     \n",
    "\n",
    "    ** create a 0-observation template data set used for assigning \n",
    "    ** variable attributes to the actual data sets;\n",
    "    data EMPTY_&dataset;\n",
    "        %do i=1 %to &vars;           \n",
    "           attrib &&var&i label=\"&&label&i\" \n",
    "             %if \"&&length&i\" ne \"\" %then\n",
    "               length=&&type&i.&&length&i... ;\n",
    "           ;\n",
    "           %if &&type&i=$ %then\n",
    "             retain &&var&i '';\n",
    "           %else\n",
    "             retain &&var&i .;\n",
    "           ;\n",
    "        %end;\n",
    "        if 0;\n",
    "    run;\n",
    "\n",
    "%mend make_empty_dataset;\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a2585cda",
   "metadata": {},
   "outputs": [],
   "source": [
    "proc sort\n",
    "  data = adam.adsl\n",
    "  (keep = usubjid siteid country age agegr1 agegr1n sex race trtsdt trt01a trt01an saffl)\n",
    "  out = adsl;\n",
    "    by usubjid;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1bb77906",
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b24c8bad",
   "metadata": {},
   "outputs": [],
   "source": [
    "data adae;\n",
    "  length relgr1 $15.;\n",
    "  merge sdtm.ae (in = inae) adsl (in = inadsl);\n",
    "    by usubjid ;\n",
    "    \n",
    "        if inae and not inadsl then\n",
    "          put 'PROB' 'LEM: Subject missing from ADSL?-- ' usubjid= inae= inadsl= ;\n",
    "        \n",
    "        rename trt01a    = trta\n",
    "               trt01an   = trtan\n",
    "        ;               \n",
    "        if inadsl and inae;\n",
    "        \n",
    "        %dtc2dt(aestdtc, prefix=ast, refdt=trtsdt);\n",
    "        %dtc2dt(aeendtc, prefix=aen, refdt=trtsdt);\n",
    "\n",
    "        if index(AEDECOD, 'PAIN')>0 or AEDECOD='HEADACHE' then\n",
    "          CQ01NAM = 'PAIN EVENT';\n",
    "        else\n",
    "          CQ01NAM = '          ';\n",
    "          \n",
    "        aereln = input(put(aerel, $aereln.), best.);\n",
    "        aesevn = input(put(aesev, $aesevn.), best.);\n",
    "        relgr1n = (aereln>0); ** group related events (AERELN>0);\n",
    "        relgr1  = put(relgr1n, relgr1n.);\n",
    "        \n",
    "        * Event is considered treatment emergent if it started on or after ;\n",
    "        * the treatment start date.  Assume treatment emergent if the start;\n",
    "        * date is missing (and the end date is either also missing or on or;\n",
    "        *  after the treatment start date)                                 ;\n",
    "        trtemfl = put((astdt>=trtsdt or (astdt<=.z  and not(.z<aendt<trtsdt))), _0n1y.);\n",
    "        if astdt>=trtsdt then\n",
    "          trtemfl = 'Y';\n",
    "        format astdt aendt yymmdd10.;\n",
    "run;\n",
    "\n",
    "\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "93c437f7",
   "metadata": {},
   "outputs": [],
   "source": [
    "** assign variable order and labels;\n",
    "data adae;\n",
    "  retain &adaeKEEPSTRING;\n",
    "  set EMPTY_adae adae;\n",
    "run;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7f63916f",
   "metadata": {},
   "outputs": [],
   "source": [
    "**** SORT adae ACCORDING TO METADATA AND SAVE PERMANENT DATASET;\n",
    "%make_sort_order(metadatafile=&metadatafile, dataset=ADAE);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d61e9f19",
   "metadata": {},
   "outputs": [],
   "source": [
    "proc sort\n",
    "  data=adae(keep = &adaeKEEPSTRING)\n",
    "  out=adam.adae;\n",
    "    by &adaeSORTSTRING;\n",
    "run;        \n",
    "\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SAS",
   "language": "sas",
   "name": "sas"
  },
  "language_info": {
   "codemirror_mode": "sas",
   "file_extension": ".sas",
   "mimetype": "text/x-sas",
   "name": "sas"
  },
  "toc-autonumbering": false,
  "toc-showcode": false,
  "toc-showmarkdowntxt": true
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
